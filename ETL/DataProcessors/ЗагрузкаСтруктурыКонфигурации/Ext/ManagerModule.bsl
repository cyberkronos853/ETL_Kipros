#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ВыполнитьЗагрузкуМетаданных(ОбщиеПеременные, АдресВременногоХранилища) Экспорт
	ОбщиеПеременные.Вставить("ЗагрузитьНовую", (ОбщиеПеременные.СпособЗагрузки = 0));
	
	КоличествоОбъектов = 0;
	КоличествоСвойств = 0;
	КоличествоЗначений = 0;
	
	мКоличествоЭлементовДляОбновленияСтатуса = 15;
	ОбщиеПеременные.Вставить("СоответствиеУИ", Новый Соответствие);
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ОбщиеПеременные.ИмяФайлаЗагрузки);
	
	ЧтениеXML.Прочитать();
	
	Если ЧтениеXML.Имя = "Конфигурация" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
		
	Иначе
		Сообщ = Новый СообщениеПользователю;
		Сообщ.Текст = НСтр("ru='Файл не содержит описания конфигурации'");
		Сообщ.Сообщить();
		ПоместитьВоВременноеХранилище(Ложь, АдресВременногоХранилища);
		Возврат;
	КонецЕсли;
	
	ЧтениеXML.Прочитать();
	
	Пока ВозможностьЧтенияXML(ЧтениеXML) Цикл
		
		ТипXML = ПолучитьXMLТип(ЧтениеXML);
		
		Если ТипXML.ИмяТипа = "CatalogObject.Базы" Тогда
			
			ПрочитатьКонфигурациюXML(ЧтениеXML, ОбщиеПеременные, Ложь);

		ИначеЕсли ТипXML.ИмяТипа = "CatalogObject.СтруктураМетаданных" Тогда
			
			ИдентифицироватьОбъектXML(ЧтениеXML, (КоличествоОбъектов - 1) % мКоличествоЭлементовДляОбновленияСтатуса = 0, ОбщиеПеременные);
			КоличествоОбъектов = КоличествоОбъектов + 1; 
						
		Иначе
			
			ПрочитатьXML(ЧтениеXML);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЧтениеXML.Закрыть(); 
		
	Если ОбщиеПеременные.ЗагрузитьНовую <> Истина
		И НЕ ОбщиеПеременные.ДобавлятьТолькоНовые Тогда
		
		Для каждого Объект Из ОбщиеПеременные.ОбъектыКонфигурации Цикл
			
			Если Объект.Ссылка.ПометкаУдаления Тогда
				Продолжить;
			КонецЕсли;
			
			УдаленныйОбъект = Объект.Ссылка.ПолучитьОбъект();
			УдаленныйОбъект.УстановитьПометкуУдаления(Истина);
			
		КонецЦикла;
		
	КонецЕсли;  
	
	Попытка
		УдалитьФайлы(ОбщиеПеременные.ИмяФайлаЗагрузки);
	Исключение
	КонецПопытки;
	
	ПоместитьВоВременноеХранилище(Истина, АдресВременногоХранилища);
	
КонецПроцедуры 

Функция глТипыСвойстваСтрокой(Свойство) 

    Рез = "";

	Типы = Свойство.Типы;
		
	Для каждого Стр Из Типы Цикл

		ИмяТипа = СокрЛП(Стр.Тип);
		
		Если ИмяТипа = "Строка" Тогда
			
			Если Свойство.КвалификаторыСтроки_Длина = 0 Тогда
				ИмяТипа = ИмяТипа + " (Неогр)";
			Иначе
				Если Свойство.КвалификаторыСтроки_Фиксированная Тогда
					ИмяТипа = ИмяТипа + " (Ф" + Свойство.КвалификаторыСтроки_Длина + ")";
				Иначе
					ИмяТипа = ИмяТипа + " (П" + Свойство.КвалификаторыСтроки_Длина + ")";
				КонецЕсли; 
			КонецЕсли; 
			
		ИначеЕсли ИмяТипа = "Число" Тогда
			
			ИмяТипа = ИмяТипа + " (" + Свойство.КвалификаторыЧисла_Длина + "." + Свойство.КвалификаторыЧисла_Точность + ")";
			
		ИначеЕсли ИмяТипа = "Дата" Тогда
			
			ИмяТипа = Свойство.КвалификаторыДаты_Состав;
			Если ПустаяСтрока(ИмяТипа) Тогда
				
				ИмяТипа = "Дата";
				
			КонецЕсли;
			
		ИначеЕсли Свойство.ЭтоГруппа Тогда
			ИмяТипа = СтрЗаменить(ИмяТипа, "РегистрСведенийЗапись",		"РегистрСведенийНаборЗаписей");
			ИмяТипа = СтрЗаменить(ИмяТипа, "РегистрНакопленияЗапись",	"РегистрНакопленияНаборЗаписей");
			ИмяТипа = СтрЗаменить(ИмяТипа, "РегистрБухгалтерииЗапись",	"РегистрБухгалтерииНаборЗаписей");
			ИмяТипа = СтрЗаменить(ИмяТипа, "РегистрРасчетаЗапись",		"РегистрРасчетаНаборЗаписей");
		КонецЕсли; 
		
        Рез = Рез + ", " + ИмяТипа;
		
	КонецЦикла; 

	Возврат Прав(Рез, СтрДлина(Рез)-2);
	
КонецФункции 

Процедура ПрочитатьКонфигурациюXML(ЧтениеXML, ОбщиеПеременные, НужноМенятьНаименованиеКонфигурации = Истина)
	
	Перем Конфигурация;
	
	Если ЧтениеXML.ТипУзла <> ТипУзлаXML.НачалоЭлемента Тогда
		ВызватьИсключение НСтр("ru='Ошибка чтения XML'");
	КонецЕсли;
	
	Если ОбщиеПеременные.ЗагрузитьНовую Тогда
		
		Конфигурация = ПрочитатьXML(ЧтениеXML);
		
		Конфигурация.Записать();
		
		ОбщиеПеременные.Вставить("Конфигурация", Конфигурация.Ссылка);
		
		Возврат;
		
	КонецЕсли;
		
	ЧтениеXML.Прочитать();
	
	// Ref
	КонфигурацияСсылка = ПрочитатьXML(ЧтениеXML, Тип("СправочникСсылка.Базы"));
	
	// IsFolder
	ЭтоГруппа = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
	
	// DeletionMark
	ПометкаУдаления = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
	
	// Parent
	Родитель = ПрочитатьXML(ЧтениеXML, Тип("СправочникСсылка.Базы"));
	
	// Description
	Наименование = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
	
	// Имя
	Имя = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
	
	// Синоним
	Синоним = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
	
	// Комментарий
	Комментарий = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
	
	// ДатаОбновления
	Версия = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
	
	// ДатаОбновления
	ДатаОбновления = ПрочитатьXML(ЧтениеXML, Тип("Дата"));
		
	Если ЧтениеXML.ТипУзла <> ТипУзлаXML.КонецЭлемента Тогда
		ВызватьИсключение НСтр("ru='Ошибка чтения XML'");
	КонецЕсли;
	
	ЧтениеXML.Прочитать();
	
	Если ОбщиеПеременные.Конфигурация.Пустая() Тогда
		
		Если ЭтоГруппа = Истина Тогда
			
			Конфигурация = Справочники.Конфигурации.СоздатьГруппу();
			
		Иначе
			
			Конфигурация = Справочники.Конфигурации.СоздатьЭлемент();
			
		КонецЕсли;
		
		Конфигурация.УстановитьСсылкуНового(КонфигурацияСсылка);
		
	Иначе
		
		Конфигурация = ОбщиеПеременные.Конфигурация.ПолучитьОбъект();
		УстановитьСоответствиеУИ(КонфигурацияСсылка.УникальныйИдентификатор(), ОбщиеПеременные.Конфигурация.УникальныйИдентификатор(), ОбщиеПеременные);
		
	КонецЕсли;
	
	Если НужноМенятьНаименованиеКонфигурации
		ИЛИ ПустаяСтрока(Конфигурация.Наименование) Тогда
		
		// Наименование
		Конфигурация.Наименование = Наименование;
		
	КонецЕсли;
		
	Конфигурация.ПометкаУдаления = ПометкаУдаления;
			
	// Версия
	//Конфигурация.Версия = Версия;
	//
	// ДатаОбновления
	//Конфигурация.ДатаОбновления = ДатаОбновления;
		
	Конфигурация.Записать();
	
	ОбщиеПеременные.Вставить("Конфигурация", Конфигурация.Ссылка);
	
	ПолучитьОбъектыКонфигурации(Конфигурация.Ссылка, ОбщиеПеременные);
	
КонецПроцедуры 

Процедура ИдентифицироватьОбъектXML(ЧтениеXML, ОбновлятьСостояние, ОбщиеПеременные)
	
	Перем Ссылка;
	
	Если ЧтениеXML.ТипУзла <> ТипУзлаXML.НачалоЭлемента Тогда
		ВызватьИсключение НСтр("ru='Ошибка чтения XML'");
	КонецЕсли;
	
	Если ОбщиеПеременные.ЗагрузитьНовую Тогда
		
		ПрочитатьXML(ЧтениеXML, Тип("Строка")); 
		Ссылка = ПрочитатьXML(ЧтениеXML); 
		Ссылка.ТипыСтрокой = глТипыСвойстваСтрокой(Ссылка);
		Ссылка.Записать();
		
		Возврат;
		
	КонецЕсли;
	
	ЧтениеXML.Прочитать();
	
	// Представление агрегатного типа
	ПредставлениеАгрегатногоТипа = ПрочитатьXML(ЧтениеXML, Тип("Строка")); 
	Если НЕ СтрНайти(ПредставлениеАгрегатногоТипа, "Объект") = 0 Тогда
		ПрочитатьОбъектXML(ЧтениеXML, ОбновлятьСостояние, ОбщиеПеременные)
		//КоличествоОбъектов = КоличествоОбъектов + 1; 
	ИначеЕсли НЕ СтрНайти(ПредставлениеАгрегатногоТипа, "Свойство") = 0 Тогда
		ПрочитатьСвойствоXML(ЧтениеXML, ОбновлятьСостояние, ОбщиеПеременные)
		//КоличествоСвойств = КоличествоСвойств + 1; 
	ИначеЕсли НЕ СтрНайти(ПредставлениеАгрегатногоТипа, "Значение") = 0 Тогда
		ПрочитатьЗначениеXML(ЧтениеXML, ОбновлятьСостояние, ОбщиеПеременные)
		//КоличествоЗначений= КоличествоЗначений + 1; 
	КонецЕсли;   
		
КонецПроцедуры 

Процедура ПрочитатьОбъектXML(ЧтениеXML, ОбновлятьСостояние, ОбщиеПеременные)
	
	Перем Объект; 
	Перем Иерархический, СерииКодов, ВидИерархии, ОграничиватьКоличествоУровней, КоличествоУровней, Подчиненный, Периодичность, КонтрольУникальности, АвтоНумерация;
		
	// Ref
	ОбъектСсылка = ПрочитатьXML(ЧтениеXML, Тип("СправочникСсылка.СтруктураМетаданных"));
	
	// IsFolder
	ЭтоГруппа = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
	
	// DeletionMark
	ПометкаУдаления = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
	
	// Owner
	Владелец = ПолучитьСсылкуНаОбъект(ПрочитатьXML(ЧтениеXML), ОбщиеПеременные);
	
	// Parent
	Родитель = ПолучитьСсылкуНаОбъект(ПрочитатьXML(ЧтениеXML, Тип("СправочникСсылка.СтруктураМетаданных")), ОбщиеПеременные);
	
	// Description
	Наименование = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
	
	// Имя
	Имя = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
	
	// Синоним
	Синоним = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
	
	// Комментарий
	Комментарий = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
		
	// Тип
	Тип = ПрочитатьXML(ЧтениеXML, Тип("ПеречислениеСсылка.ТипыОбъектов"));     
		
	Если Тип = Перечисления.ТипыОбъектов.Справочник
		ИЛИ Тип = Перечисления.ТипыОбъектов.Документ
		ИЛИ Тип = Перечисления.ТипыОбъектов.ПланВидовХарактеристик Тогда
		
		Если Тип = Перечисления.ТипыОбъектов.Справочник
			ИЛИ Тип = Перечисления.ТипыОбъектов.ПланВидовХарактеристик Тогда 
			
			// Иерархический
			Иерархический = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
			
			// СерииКодов
			СерииКодов = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
			
			Если Тип = Перечисления.ТипыОбъектов.Справочник Тогда 
				
				// ВидИерархии
				ВидИерархии = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				
				// ОграничиватьКоличествоУровней
				ОграничиватьКоличествоУровней = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
				
				// КоличествоУровней
				КоличествоУровней = ПрочитатьXML(ЧтениеXML, Тип("Число"));  
				
				// Подчиненный
				Подчиненный = ПрочитатьXML(ЧтениеXML, Тип("Булево"));  
				
			КонецЕсли;
			
		ИначеЕсли Тип = Перечисления.ТипыОбъектов.Документ Тогда
			
			// Периодичность
			Периодичность = ПрочитатьXML(ЧтениеXML, Тип("Строка")); 
			
		КонецЕсли;
		
		// КонтрольУникальности
		КонтрольУникальности = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
		
		// АвтоНумерация
		АвтоНумерация = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
		
	КонецЕсли;
	
	Если ЧтениеXML.ТипУзла <> ТипУзлаXML.КонецЭлемента Тогда
		ВызватьИсключение НСтр("ru='Ошибка чтения XML'");
	КонецЕсли;
	
	ЧтениеXML.Прочитать();
	
	мЗапросОбъекты = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                              |	Ссылка КАК Объект
	                              |ИЗ
	                              |	Справочник.СтруктураМетаданных КАК Т
	                              |ГДЕ
	                              |	Имя = &Имя
	                              |	И Владелец = &Владелец
	                              |	И Родитель = &Родитель
	                              |	И ЭтоГруппа = &ЭтоГруппа
								  //Объект по виду не искать!!!
								  |	И Вид = ЗНАЧЕНИЕ(Перечисление.ВидыСвойств.ПустаяСсылка)
								  //
	                              |	И Тип = &Тип");
	
	мЗапросОбъекты.УстановитьПараметр("Имя", Имя);
	мЗапросОбъекты.УстановитьПараметр("Владелец", Владелец);
	мЗапросОбъекты.УстановитьПараметр("Родитель", Родитель);
	мЗапросОбъекты.УстановитьПараметр("ЭтоГруппа", ЭтоГруппа);
	мЗапросОбъекты.УстановитьПараметр("Тип", ?(Тип = Неопределено, Перечисления.ТипыОбъектов.ПустаяСсылка(), Тип));
		
	РезультатЗапроса = мЗапросОбъекты.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Если ЭтоГруппа = Истина Тогда
			Объект = Справочники.СтруктураМетаданных.СоздатьГруппу();
		Иначе
			Объект = Справочники.СтруктураМетаданных.СоздатьЭлемент();
		КонецЕсли;
		
		Объект.УстановитьСсылкуНового(ОбъектСсылка);
		
		ЗаписыватьЗначение = Истина;
		
	Иначе
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
				
		УстановитьСоответствиеУИ(ОбъектСсылка.УникальныйИдентификатор(), Выборка.Объект.УникальныйИдентификатор(), ОбщиеПеременные);
		
		Если ОбщиеПеременные.ДобавлятьТолькоНовые Тогда 
			Возврат;
		КонецЕсли;
		
		Объект = Выборка.Объект.ПолучитьОбъект();
		ЗаписыватьЗначение = Ложь;
		
	КонецЕсли;
	
	// Владелец
	УстановитьЗначениеПараметра(Объект.Владелец, Владелец, ЗаписыватьЗначение);
	
	УстановитьЗначениеПараметра(Объект.ПометкаУдаления, ПометкаУдаления, ЗаписыватьЗначение);
				
	// Родитель
	УстановитьЗначениеПараметра(Объект.Родитель, Родитель, ЗаписыватьЗначение);
		
	// Наименование
	УстановитьЗначениеПараметра(Объект.Наименование, Наименование, ЗаписыватьЗначение);
		
	// Имя
	УстановитьЗначениеПараметра(Объект.Имя, Имя, ЗаписыватьЗначение);
		
	// Синоним
	УстановитьЗначениеПараметра(Объект.Синоним, Синоним, ЗаписыватьЗначение);
		
	// Комментарий
	УстановитьЗначениеПараметра(Объект.Комментарий, Комментарий, ЗаписыватьЗначение);
		
	// Тип
	УстановитьЗначениеПараметра(Объект.Тип, Тип, ЗаписыватьЗначение);
							
	// Иерархический
	УстановитьЗначениеПараметра(Объект.Иерархический, Иерархический, ЗаписыватьЗначение);
	
	// СерииКодов
	//УстановитьЗначениеПараметра(Объект.СерииКодов, СерииКодов, ЗаписыватьЗначение);
	
	// ВидИерархии
	УстановитьЗначениеПараметра(Объект.ВидИерархии, ВидИерархии, ЗаписыватьЗначение);
	
	// ОграничиватьКоличествоУровней
	УстановитьЗначениеПараметра(Объект.ОграничиватьКоличествоУровней, ОграничиватьКоличествоУровней, ЗаписыватьЗначение);
	
	// КоличествоУровней
	УстановитьЗначениеПараметра(Объект.КоличествоУровней, КоличествоУровней, ЗаписыватьЗначение);
	
	// Подчиненный
	УстановитьЗначениеПараметра(Объект.Подчиненный, Подчиненный, ЗаписыватьЗначение);  
	
	// Периодичность
	УстановитьЗначениеПараметра(Объект.Периодичность, Периодичность, ЗаписыватьЗначение);
	
	// КонтрольУникальности
	УстановитьЗначениеПараметра(Объект.КонтрольУникальности, КонтрольУникальности, ЗаписыватьЗначение);
	
	// АвтоНумерация
	УстановитьЗначениеПараметра(Объект.АвтоНумерация, АвтоНумерация, ЗаписыватьЗначение);
							
	Если ЗаписыватьЗначение Тогда
		Объект.Записать();
	КонецЕсли;
	
	//КоличествоОбъектов = КоличествоОбъектов + 1; 
	
	УдалитьИзОбъектовКонфигурации(Объект.Ссылка, ОбщиеПеременные);		
	
КонецПроцедуры 

Процедура ПрочитатьСвойствоXML(ЧтениеXML, ОбновлятьСостояние, ОбщиеПеременные)
	
	Перем Свойство;
	
	// Ref
	СвойствоСсылка = ПрочитатьXML(ЧтениеXML, Тип("СправочникСсылка.СтруктураМетаданных"));

	// IsFolder
	ЭтоГруппа = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
	
	// DeletionMark
	ПометкаУдаления = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
	
	// Owner
	Владелец = ПолучитьСсылкуНаОбъект(ПрочитатьXML(ЧтениеXML), ОбщиеПеременные);
	
	// Parent
	Родитель = ПолучитьСсылкуНаОбъект(ПрочитатьXML(ЧтениеXML, Тип("СправочникСсылка.СтруктураМетаданных")), ОбщиеПеременные);
	
	// Code
	Код = ПрочитатьXML(ЧтениеXML, Тип("Число"));
	
	// Description
	Наименование = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
	
	// Синоним
	Синоним = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
	
	// Комментарий
	Комментарий = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
		
	Если ЭтоГруппа = Ложь Тогда
		
		// Использование
		Использование = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
		
		// Индексирование
		Индексирование = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
		
		// КвалификаторыЧисла_Длина
		КвалификаторыЧисла_Длина = ПрочитатьXML(ЧтениеXML, Тип("Число"));
		
		// КвалификаторыЧисла_Точность
		КвалификаторыЧисла_Точность = ПрочитатьXML(ЧтениеXML, Тип("Число"));
		
		// КвалификаторыЧисла_Неотрицательное
		КвалификаторыЧисла_Неотрицательное = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
		
		// КвалификаторыСтроки_Длина
		КвалификаторыСтроки_Длина = ПрочитатьXML(ЧтениеXML, Тип("Число"));
		
		// КвалификаторыСтроки_Фиксированная
		КвалификаторыСтроки_Фиксированная = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
		
		// КвалификаторыДаты_Состав
		КвалификаторыДаты_Состав = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
		
		// Авторегистрация
		Авторегистрация = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
		
	КонецЕсли;
	
	// Вид
	Вид = ПрочитатьXML(ЧтениеXML, Тип("ПеречислениеСсылка.ВидыСвойств"));
	
	// Тип
	Тип = ПрочитатьXML(ЧтениеXML, Тип("ПеречислениеСсылка.ТипыОбъектов"));
	
	// ТипыСтрокой
	ТипыСтрокой = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
		
	Типы = Новый Массив;
	
	// Типы
	Если ЧтениеXML.Имя = "Типы" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
		
		ЧтениеXML.Прочитать();
		
		Пока ЧтениеXML.Имя = "Row" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Цикл
			
			ЧтениеXML.Прочитать();
			
			Типы.Добавить(ПолучитьСсылкуНаОбъект(ПрочитатьXML(ЧтениеXML, Тип("СправочникСсылка.СтруктураМетаданных")), ОбщиеПеременные));
			
			ЧтениеXML.Прочитать();
			
		КонецЦикла;
		
		ЧтениеXML.Прочитать();
		
	КонецЕсли;
	
	Если ЧтениеXML.ТипУзла <> ТипУзлаXML.КонецЭлемента Тогда
		ВызватьИсключение НСтр("ru='Ошибка чтения XML'");
	КонецЕсли;
	
	ЧтениеXML.Прочитать();
	
	ЭтоЭлементСоставаПланаОбмена = Вид = Перечисления.ВидыСвойств.ЭлементСоставаПланаОбмена И Наименование <> "{Состав}";
	
	ЗапросСвойства = Новый Запрос("ВЫБРАТЬ
	                              |	Ссылка КАК Свойство
	                              |ИЗ
	                              |	Справочник.СтруктураМетаданных КАК Т
	                              |ГДЕ
	                              |	Наименование = &Наименование
	                              |	И Владелец = &Владелец
	                              |	И Родитель = &Родитель
	                              |	И ЭтоГруппа = &ЭтоГруппа
	                              |	И Вид = &Вид
	                              |	И Тип = &Тип");
	
	Если ЭтоЭлементСоставаПланаОбмена Тогда
		
		ЗапросСвойства.Текст = ЗапросСвойства.Текст + "
													|	И Типы.Тип = &Тип";
		ЗапросСвойства.УстановитьПараметр("Тип", Типы[0]);
				
	КонецЕсли;
	
	ЗапросСвойства.УстановитьПараметр("Наименование", Наименование);
	ЗапросСвойства.УстановитьПараметр("Владелец", Владелец);
	ЗапросСвойства.УстановитьПараметр("Родитель", Родитель);
	ЗапросСвойства.УстановитьПараметр("ЭтоГруппа", ЭтоГруппа);
	ЗапросСвойства.УстановитьПараметр("Вид", Вид);
	ЗапросСвойства.УстановитьПараметр("Тип", Тип);
	
	РезультатЗапроса = ЗапросСвойства.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Если ЭтоГруппа = Истина Тогда
			Свойство = Справочники.СтруктураМетаданных.СоздатьГруппу();
		Иначе
			Свойство = Справочники.СтруктураМетаданных.СоздатьЭлемент();
		КонецЕсли;
		
		Свойство.УстановитьСсылкуНового(СвойствоСсылка);
		
		ЗаписыватьЗначение = Истина;
		
	Иначе
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		СвойствоВыборки = Выборка.Свойство;
		
		УстановитьСоответствиеУИ(СвойствоСсылка.УникальныйИдентификатор(), СвойствоВыборки.УникальныйИдентификатор(), ОбщиеПеременные);
		
		Если ОбщиеПеременные.ДобавлятьТолькоНовые Тогда
			Возврат;
		КонецЕсли;
		
		Свойство = СвойствоВыборки.ПолучитьОбъект();
		
		ЗаписыватьЗначение = Ложь;
		
	КонецЕсли;
	
	// Владелец
	УстановитьЗначениеПараметра(Свойство.Владелец, Владелец, ЗаписыватьЗначение);
	
	УстановитьЗначениеПараметра(Свойство.ПометкаУдаления, ПометкаУдаления, ЗаписыватьЗначение);
			
	// Родитель
	УстановитьЗначениеПараметра(Свойство.Родитель, Родитель, ЗаписыватьЗначение);
		
	// Код
	//Свойство.Код = Код; // это свойство ни на что не влияет
			
	// Наименование
	УстановитьЗначениеПараметра(Свойство.Наименование, Наименование, ЗаписыватьЗначение);
	
	// Имя, справочно
	УстановитьЗначениеПараметра(Свойство.Имя, Наименование, ЗаписыватьЗначение);
	
	// Синоним
	УстановитьЗначениеПараметра(Свойство.Синоним, Синоним, ЗаписыватьЗначение);
		
	// Комментарий
	УстановитьЗначениеПараметра(Свойство.Комментарий, Комментарий, ЗаписыватьЗначение);
				
	// Следующие реквизиты для группы не определены
	Если ЭтоГруппа = Ложь Тогда
		
		// Использование
		УстановитьЗначениеПараметра(Свойство.Использование, Использование, ЗаписыватьЗначение);
				
		// Индексирование
		УстановитьЗначениеПараметра(Свойство.Индексирование, Индексирование, ЗаписыватьЗначение);
				
		// КвалификаторыЧисла_Длина
		УстановитьЗначениеПараметра(Свойство.КвалификаторыЧисла_Длина, КвалификаторыЧисла_Длина, ЗаписыватьЗначение);
				
		// КвалификаторыЧисла_Точность
		УстановитьЗначениеПараметра(Свойство.КвалификаторыЧисла_Точность, КвалификаторыЧисла_Точность, ЗаписыватьЗначение);
				
		// КвалификаторыЧисла_Неотрицательное
		УстановитьЗначениеПараметра(Свойство.КвалификаторыЧисла_Неотрицательное, КвалификаторыЧисла_Неотрицательное, ЗаписыватьЗначение);
				
		// КвалификаторыСтроки_Длина
		УстановитьЗначениеПараметра(Свойство.КвалификаторыСтроки_Длина, КвалификаторыСтроки_Длина, ЗаписыватьЗначение);
				
		// КвалификаторыСтроки_Фиксированная
		УстановитьЗначениеПараметра(Свойство.КвалификаторыСтроки_Фиксированная, КвалификаторыСтроки_Фиксированная, ЗаписыватьЗначение);
				
		// КвалификаторыДаты_Состав
		УстановитьЗначениеПараметра(Свойство.КвалификаторыДаты_Состав, КвалификаторыДаты_Состав, ЗаписыватьЗначение);
				
		// Авторегистрация
		УстановитьЗначениеПараметра(Свойство.Авторегистрация, Авторегистрация, ЗаписыватьЗначение);
				
	КонецЕсли;
	
	// Вид
	УстановитьЗначениеПараметра(Свойство.Вид, Вид, ЗаписыватьЗначение);  
	
	// Тип
	УстановитьЗначениеПараметра(Свойство.Тип, Тип, ЗаписыватьЗначение);
		
	// Типы
	Если Свойство.Типы.Количество() <> Типы.Количество() Тогда
		
		ЗаписыватьЗначение = Истина;
		
		Свойство.Типы.Очистить();
		
		Для Индекс = 0 По Типы.Количество() - 1 Цикл
			
			Свойство.Типы.Добавить().Тип = Типы[Индекс];
			
		КонецЦикла;
		
	Иначе
		
		Для Индекс = 0 По Типы.Количество() - 1 Цикл
			
			УстановитьЗначениеПараметра(Свойство.Типы[Индекс].Тип, Типы[Индекс], ЗаписыватьЗначение);
						
		КонецЦикла;
		
	КонецЕсли;
	
	// ТипыСтрокой
	УстановитьЗначениеПараметра(Свойство.ТипыСтрокой, глТипыСвойстваСтрокой(Свойство), ЗаписыватьЗначение);
	
	Если ЗаписыватьЗначение Тогда
		Свойство.Записать();
	КонецЕсли;
	
	//КоличествоСвойств = КоличествоСвойств + 1;
	
	УдалитьИзОбъектовКонфигурации(Свойство.Ссылка, ОбщиеПеременные);		
	
КонецПроцедуры 

Процедура ПрочитатьЗначениеXML(ЧтениеXML, ОбновлятьСостояние, ОбщиеПеременные)
	
	Перем Значение;
		
	// Ref
	ЗначениеСсылка = ПрочитатьXML(ЧтениеXML, Тип("СправочникСсылка.СтруктураМетаданных"));

	// DeletionMark
	ПометкаУдаления = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
	
	// Owner
	Владелец = ПолучитьСсылкуНаОбъект(ПрочитатьXML(ЧтениеXML), ОбщиеПеременные);
	
	// Parent
	Родитель = ПолучитьСсылкуНаОбъект(ПрочитатьXML(ЧтениеXML, Тип("СправочникСсылка.СтруктураМетаданных")), ОбщиеПеременные);
	
	// Code
	Код = ПрочитатьXML(ЧтениеXML, Тип("Число"));
	
	// Description
	Наименование = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
	
	// Синоним
	Синоним = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
	
	// Комментарий
	Комментарий = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
	
	// Предопределенное
	Предопределенное = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
	
	// Вид
	Вид = ПрочитатьXML(ЧтениеXML, Тип("ПеречислениеСсылка.ВидыСвойств"));
	
	// Тип
	Тип = ПрочитатьXML(ЧтениеXML, Тип("ПеречислениеСсылка.ТипыОбъектов"));
	
	// Типы
	ЧтениеXML.Прочитать();
	ЧтениеXML.Прочитать();
	
	Если ЧтениеXML.ТипУзла <> ТипУзлаXML.КонецЭлемента Тогда
		ВызватьИсключение НСтр("ru='Ошибка чтения XML'");
	КонецЕсли;
	
	ЧтениеXML.Прочитать(); 
	
	мЗапросЗначения = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                               |	Ссылка КАК Значение
	                               |ИЗ
	                               |	Справочник.СтруктураМетаданных КАК Т
	                               |ГДЕ
	                               |	Наименование = &Наименование
	                               |	И Владелец = &Владелец
	                               |	И Родитель = &Родитель
	                               |	И НЕ ЭтоГруппа
	                               |	И Вид = &Вид
	                               |	И Тип = &Тип");
								   
	мЗапросЗначения.УстановитьПараметр("Наименование", Наименование);
	мЗапросЗначения.УстановитьПараметр("Владелец", Владелец);
	мЗапросЗначения.УстановитьПараметр("Родитель", Родитель);
	мЗапросЗначения.УстановитьПараметр("Вид", Вид);
	мЗапросЗначения.УстановитьПараметр("Тип", Тип);
	
	РезультатЗапроса = мЗапросЗначения.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Значение = Справочники.СтруктураМетаданных.СоздатьЭлемент();
		
		Значение.УстановитьСсылкуНового(ЗначениеСсылка);
		ЗаписыватьЗначение = Истина;
		
	Иначе
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
				
		УстановитьСоответствиеУИ(ЗначениеСсылка.УникальныйИдентификатор(), Выборка.Значение.УникальныйИдентификатор(), ОбщиеПеременные);
		
		Если ОбщиеПеременные.ДобавлятьТолькоНовые Тогда 
			Возврат;
		КонецЕсли;

		Значение = Выборка.Значение.ПолучитьОбъект();
		ЗаписыватьЗначение = Ложь;
		
	КонецЕсли;
	
	// Владелец
	УстановитьЗначениеПараметра(Значение.Владелец, Владелец, ЗаписыватьЗначение);
	
	УстановитьЗначениеПараметра(Значение.ПометкаУдаления, ПометкаУдаления, ЗаписыватьЗначение);
				
	// Родитель
	УстановитьЗначениеПараметра(Значение.Родитель, Родитель, ЗаписыватьЗначение);
		
	// Код
	//Значение.Код = Код;
			
	// Наименование
	УстановитьЗначениеПараметра(Значение.Наименование, Наименование, ЗаписыватьЗначение);
	
	// Имя, справочно
	УстановитьЗначениеПараметра(Значение.Имя, Наименование, ЗаписыватьЗначение);
	
	// Синоним
	УстановитьЗначениеПараметра(Значение.Синоним, Синоним, ЗаписыватьЗначение);
		
	// Комментарий
	УстановитьЗначениеПараметра(Значение.Комментарий, Комментарий, ЗаписыватьЗначение);
		
	// Предопределенное
	УстановитьЗначениеПараметра(Значение.Предопределенное, Предопределенное, ЗаписыватьЗначение);
	
	// Вид
	УстановитьЗначениеПараметра(Значение.Вид, Вид, ЗаписыватьЗначение);
	
	// Тип
	УстановитьЗначениеПараметра(Значение.Тип, Тип, ЗаписыватьЗначение);
	
	Если ЗаписыватьЗначение Тогда
		Значение.Записать();
	КонецЕсли;
	
	УдалитьИзОбъектовКонфигурации(Значение.Ссылка, ОбщиеПеременные);		
	
КонецПроцедуры 

Процедура УстановитьЗначениеПараметра(ТекущийПараметр, НовыйПараметр, ЗначениеИзменено)
	
	Если ТекущийПараметр <> НовыйПараметр Тогда
		ТекущийПараметр = НовыйПараметр;
		ЗначениеИзменено = Истина;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьСсылкуНаОбъект(Объект, ОбщиеПеременные)
	
	УникальныйИдентификатор = ОбщиеПеременные.СоответствиеУИ[Строка(Объект.УникальныйИдентификатор())];
	
	Если УникальныйИдентификатор = Неопределено Тогда
		Возврат Объект;
	Иначе
		
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.УстановитьСтроку("<Ref>" + УникальныйИдентификатор + "</Ref>");
		
		Возврат ПрочитатьXML(ЧтениеXML, ТипЗнч(Объект));
		
	КонецЕсли;
	
КонецФункции 

Процедура УстановитьСоответствиеУИ(УИЗагруженный, УИСуществующий, ОбщиеПеременные)
	
	ОбщиеПеременные.СоответствиеУИ.Вставить(Строка(УИЗагруженный), Строка(УИСуществующий));
	
КонецПроцедуры 

Процедура ПолучитьОбъектыКонфигурации(Конфигурация, ОбщиеПеременные)
	
	Если ОбщиеПеременные.ЗагрузитьНовую
		ИЛИ ОбщиеПеременные.ДобавлятьТолькоНовые Тогда
			Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Т.Ссылка
	                      |ИЗ
	                      |	Справочник.СтруктураМетаданных КАК Т
	                      |ГДЕ
	                      |	Владелец = &Конфигурация
	                      |	И ЭтоГруппа = ЛОЖЬ
	                      |	И ПометкаУдаления = ЛОЖЬ");
	
	Запрос.УстановитьПараметр("Конфигурация", Конфигурация);
	
	мОбъектыКонфигурации = Запрос.Выполнить().Выгрузить();
	
	мОбъектыКонфигурации.Индексы.Добавить("Ссылка");
	ОбщиеПеременные.Вставить("ОбъектыКонфигурации", мОбъектыКонфигурации); 
	
КонецПроцедуры 

Процедура УдалитьИзОбъектовКонфигурации(Объект, ОбщиеПеременные)
	
	Если ОбщиеПеременные.ЗагрузитьНовую
		ИЛИ ОбщиеПеременные.ДобавлятьТолькоНовые Тогда
			Возврат;
	КонецЕсли;
	
	НайденнаяСтрока = ОбщиеПеременные.ОбъектыКонфигурации.Найти(Объект, "Ссылка");
	
	Если НайденнаяСтрока <> Неопределено Тогда
		ОбщиеПеременные.ОбъектыКонфигурации.Удалить(НайденнаяСтрока);
	КонецЕсли;
	
КонецПроцедуры 

#КонецЕсли