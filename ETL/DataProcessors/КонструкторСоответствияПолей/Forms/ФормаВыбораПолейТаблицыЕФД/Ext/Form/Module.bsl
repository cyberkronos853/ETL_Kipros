&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого Реквизит Из ПолучитьРеквизиты() Цикл
		ИмяРеквизита = Реквизит.Имя;
		
		Если Параметры.Свойство(ИмяРеквизита) Тогда 
			ЭтаФорма[ИмяРеквизита] = Параметры[ИмяРеквизита];
		КонецЕсли;
		
	КонецЦикла;
		
	ОбновитьСоставПолей();
		
КонецПроцедуры  

&НаСервере
Процедура ОбновитьСоставПолей()
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Т.КлючСвязи КАК КлючСвязи,
	                      |	Т.ИмяПоля КАК ИмяПоля,
	                      |	Т.ИмяПоляSQL КАК ИмяПоляSQL,
	                      |	Т.Синоним КАК Синоним
	                      |ИЗ
	                      |	Справочник.ЕдиныйФорматДанных.СтруктураТаблицыSQL КАК Т
	                      |ГДЕ
	                      |	Т.Ссылка = &Ссылка
	                      |	И НЕ Т.КлючСвязи = 0");   
	
	Запрос.УстановитьПараметр("Ссылка", ОбъектЕдиногоФорматаДанных);    
	
	тзСтруктураТаблицыSQL.Загрузить(Запрос.Выполнить().Выгрузить());
		
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЭлемент()
	
	ТекДанные = Элементы.тзСтруктураТаблицыSQL.ТекущиеДанные;
	Закрыть(Новый Структура("ОбъектЕдиногоФорматаДанных, КлючСвязи", 
							ОбъектЕдиногоФорматаДанных,
							ТекДанные.КлючСвязи));

КонецПроцедуры

&НаКлиенте
Процедура СтруктураТаблицыSQLВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВыбратьЭлемент()
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьФокусИнтерактивногоПодбора()
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФокусИнтерактивногоПодбора() 
	
	ИмяОбъектаИтерации = "тзСтруктураТаблицыSQL";
	
	СписокПолейПоиска = Новый СписокЗначений;
	СписокПолейПоиска.Добавить("КлючСвязи");
	СписокПолейПоиска.Добавить("Синоним");
	
	КоллекцияЭлементовДерева = Элементы[ИмяОбъектаИтерации]; 
	
	Для Каждого ЗначениеСписка Из СписокПолейПоиска Цикл 
		
		ИмяПоля = ЗначениеСписка.Значение;
		
		НайденныйЭлементДерева = НайтиЭлементВКоллекции(ЭтаФорма[ИмяОбъектаИтерации], Новый Структура(ИмяПоля, ЭтаФорма[ИмяПоля]));  
		Если НЕ НайденныйЭлементДерева = Неопределено Тогда
			КоллекцияЭлементовДерева.ТекущаяСтрока = НайденныйЭлементДерева.ПолучитьИдентификатор(); 
			Прервать;
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция НайтиЭлементВКоллекции(КоллекцияЭлементовДерева, СтруктураПоиска) 
	
	ЭлементКоллекции = Неопределено;
	
	Для Каждого СтрокаКоллекции Из КоллекцияЭлементовДерева Цикл  
		
		ЭлементСоответствуетПараметрамПоиска = Истина;    
		
		Для Каждого КлючИЗначение Из СтруктураПоиска Цикл
			
			Если НЕ СтрокаКоллекции[КлючИЗначение.Ключ] = КлючИЗначение.Значение Тогда
				ЭлементСоответствуетПараметрамПоиска = Ложь;
				Прервать;
			КонецЕсли;
						
		КонецЦикла;
			
		Если ЭлементСоответствуетПараметрамПоиска Тогда
			ЭлементКоллекции = СтрокаКоллекции  
		КонецЕсли;
		
		Если НЕ ЭлементКоллекции = Неопределено Тогда 
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ЭлементКоллекции
	
КонецФункции