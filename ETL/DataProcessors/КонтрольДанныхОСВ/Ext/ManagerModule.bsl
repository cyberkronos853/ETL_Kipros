Функция ПечатьСКД(СтруктураДанных) Экспорт 
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	СхемаКомпоновкиДанных=ПолучитьМакет("скдДанныеПериода");
	НастройкиСКД = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных(); 
	ЗаполнитьПараметрыСКД(НастройкиСКД, СтруктураДанных);
	
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиСКД,,, Тип("ГенераторМакетаКомпоновкиДанных"));
	
	ВнешниеНаборыДанных = Новый Структура("тзДанные", СтруктураДанных.тзДанные);
	
	// Инициализация процессора компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных, ВнешниеНаборыДанных,, Истина);
		
	// Получение результата
	ПроцессорВыводаРезультатаКомпоновкиДанных = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	
	ПроцессорВыводаРезультатаКомпоновкиДанных.УстановитьДокумент(ТабличныйДокумент);
	ПроцессорВыводаРезультатаКомпоновкиДанных.Вывести(ПроцессорКомпоновкиДанных);
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ЗаполнитьПараметрыСКД(НастройкиСКД, СтруктураДанных); 
		
	ЗначениеПараметраНастроек = НастройкиСКД.ПараметрыВывода.Элементы.Найти(Новый ПараметрКомпоновкиДанных("Заголовок"));
	Если НЕ ЗначениеПараметраНастроек = Неопределено Тогда
		ПредставлениеПараметра = СокрЛП(ЗначениеПараметраНастроек.Значение);
		ЗначениеПараметраНастроек.Использование = Истина;
		ЗначениеПараметраНастроек.Значение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПредставлениеПараметра, СтруктураДанных.ПредставлениеПериода)
	КонецЕсли;  
	
КонецПроцедуры
