//******************************************************************************************
Функция ПолучитьАналитикуСчета(КодСчета, ТипБазы, База = Неопределено) Экспорт 
	
	Запрос = Новый Запрос;
	
	Реквизиты			 = "";
	КоличествоСубконто	 = Метаданные.Справочники.СчетУчета.ТабличныеЧасти.Аналитика.Реквизиты.Количество() - 1;
	
	Для А = 1 По КоличествоСубконто Цикл 
		
		Реквизиты = Реквизиты + "Аналитика.Субконто" + А + " КАК Субконто" + А + ",";
		
	КонецЦикла;

	Запрос.Текст = "ВЫБРАТЬ
	|	&Реквизиты
	|ИЗ
	|	Справочник.СчетУчета.Аналитика КАК Аналитика
	|ГДЕ
	|	Аналитика.Счет = &КодСчета
	|	И Аналитика.Ссылка.Владелец = &ТипБазы";

	Запрос.УстановитьПараметр("ТипБазы",	 ТипБазы);
	Запрос.УстановитьПараметр("КодСчета",	 КодСчета);
	
	Если База <> Неопределено Тогда 
		
		Запрос.Текст = Запрос.Текст + " И Аналитика.Ссылка.База = &База";
		
		Запрос.УстановитьПараметр("База", База);
		
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Реквизиты", Лев(Реквизиты, СтрДлина(Реквизиты) - 1));
	
	Результат = Запрос.Выполнить();
	
	Аналитика = Новый Структура;
	
	Если Результат.Пустой() Тогда 
		
		Возврат Аналитика;
		
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Для А = 1 По КоличествоСубконто Цикл 
			
			Если ЗначениеЗаполнено(Выборка["Субконто" + А]) Тогда 
				
				Аналитика.Вставить("Субконто" + А, Выборка["Субконто" + А]);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Аналитика;
	
КонецФункции
//******************************************************************************************
Функция ПолучитьСчет(КодСчета, ТипБазы, База = Неопределено) Экспорт 

	Запрос = Новый Запрос;

	Запрос.Текст = "ВЫБРАТЬ
	|	СчетПлана.Счет КАК Счет,
	|	СчетПлана.Ссылка КАК Ссылка,
	|	СчетПлана.Валютный КАК Валютный,
	|	СчетПлана.Активный КАК Активный,
	|	СчетПлана.Наименование КАК Наименование,
	|	СчетПлана.Количественный КАК Количественный
	|ИЗ
	|	Справочник.СчетУчета.Счета КАК СчетПлана
	|ГДЕ
	|	СчетПлана.Счет = &КодСчета
	|	И СчетПлана.Ссылка.Владелец = &ТипБазы";

	Запрос.УстановитьПараметр("ТипБазы",	 ТипБазы);
	Запрос.УстановитьПараметр("КодСчета",	 КодСчета);
	
	Если База <> Неопределено Тогда 
		
		Запрос.Текст = Запрос.Текст + " И СчетПлана.Ссылка.База = &База";
		
		Запрос.УстановитьПараметр("База", База);
		
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	
	Счет = Новый Структура("Счет, Ссылка, Активный, Валютный, Наименование, Количественный, Аналитика");
	
	Если Результат.Пустой() Тогда 
		
		Возврат Счет;
		
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(Счет, Выборка);

	Счет.Аналитика = ПолучитьАналитикуСчета(КодСчета, ТипБазы, База);
	
	Возврат Счет;
	
КонецФункции
//******************************************************************************************
Функция ПолучитьСчетПоБазе(КодСчета, База) Экспорт
	
	Возврат ПолучитьСчет(КодСчета, База.ТипБазы, База); 
	
КонецФункции
//******************************************************************************************
