
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.Банки.ПФ_MXL_КлассификаторБанков");

	Макет.Параметры.Расшифровка = Истина; // чтобы работала расшифровка

	ПолеТабличногоДокумента.Очистить();
	ПолеТабличногоДокумента.Вывести(Макет);

	ПолеТабличногоДокумента.ФиксацияСверху      = ПолеТабличногоДокумента.Области.ОбластьРасшифровки.Верх - 1;

	ПолеТабличногоДокумента.ОтображатьЗаголовки = Ложь;
	ПолеТабличногоДокумента.ОтображатьСетку     = Ложь;
	ПолеТабличногоДокумента.ТолькоПросмотр      = Истина;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ПолеТабличногоДокументаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ТабличныйДокумент = ПолеТабличногоДокумента;
	ТекущаяОбласть    = ТабличныйДокумент.ТекущаяОбласть;

	ОбластьКодБанка     		= ТабличныйДокумент.Области.КодБанка;
	ОбластьКодБанкаДоРеформы 	= ТабличныйДокумент.Области.КодБанкаДоРеформы;
	ОбластьНаименование 		= ТабличныйДокумент.Области.Наименование;
	ОбластьГород        		= ТабличныйДокумент.Области.Город;

	СтруктураДанных = Новый Структура;
	
	СтруктураДанных.Вставить("КодБанка", ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКодБанка.Лево, ТекущаяОбласть.Низ, ОбластьКодБанка.Право).Текст);
	СтруктураДанных.Вставить("КодБанкаДоРеформы", ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКодБанкаДоРеформы.Лево, ТекущаяОбласть.Низ, ОбластьКодБанкаДоРеформы.Право).Текст);
	
	СтруктураДанных.Вставить("Наименование", ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименование.Лево, ТекущаяОбласть.Низ, ОбластьНаименование.Право).Текст);
	СтруктураДанных.Вставить("Город", ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьГород.Лево, ТекущаяОбласть.Низ, ОбластьГород.Право).Текст);
	
	КодДляПоиска = "";
	
	Ссылка = ПолучитьСуществующийБанк(СтруктураДанных, КодДляПоиска);
	
	Если НЕ Ссылка.Пустая() Тогда
		ТекстВопроса = НСтр("ru='В справочнике ""Банки"" уже существует элемент с БИК %1! Открыть существующий?'");
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, 
			КодДляПоиска);
		
		Данные = Новый Структура("Ссылка, СтруктураДанных", Ссылка, СтруктураДанных);
		
		Режим = РежимДиалогаВопрос.ДаНетОтмена;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаПриПодборе", ЭтотОбъект, Данные);
		ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
		
	Иначе
		СоздатьНовыйБанк(СтруктураДанных);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаПриПодборе(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ОткрытьФорму("Справочник.Банки.Форма.ФормаЭлемента", Новый Структура("Ключ", Параметры.Ссылка));
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Функция ПолучитьСуществующийБанк(СтруктураДанных, КодДляПоиска)
	
	КодДляПоиска = СтруктураДанных.КодБанка;
	Ссылка = Справочники.Банки.НайтиПоРеквизиту("БИК", КодДляПоиска);
	
	//Если Ссылка.Пустая() И НЕ ПустаяСтрока(СтруктураДанных.КодБанкаДоРеформы) Тогда
	//	КодДляПоиска = СтруктураДанных.КодБанкаДоРеформы;
	//	Ссылка = Справочники.Банки.НайтиПоРеквизиту("БИКДоРеформыБанковскихСчетов", КодДляПоиска);
	//КонецЕсли;	
	
	Возврат Ссылка;
		
КонецФункции

&НаКлиенте
Процедура СоздатьНовыйБанк(СтруктураДанных)
	
	// Создание нового банка.
	ФормаНовогоЭлемента = ПолучитьФорму("Справочник.Банки.ФормаОбъекта", , ЭтаФорма);

	ОбъектЗаполнения = ФормаНовогоЭлемента.Объект;
	
	ОбъектЗаполнения.БИК          					= СтруктураДанных.КодБанка;
	ОбъектЗаполнения.Наименование 					= СтрПолучитьСтроку(СтруктураДанных.Наименование, 1);
	ОбъектЗаполнения.Город		 					= СтрПолучитьСтроку(СтруктураДанных.Город, 1);
	//	
	//Если НЕ ЗначениеЗаполнено(ОбъектЗаполнения.КодВПлатежнойСистеме) Тогда
	//	ОбъектЗаполнения.КодВПлатежнойСистеме = УправлениеДенежнымиСредствамиКлиентСервер.ПолучитьКодБанкаВПлатежнойСистеме(СтруктураДанных.КодБанка);
	//КонецЕсли;
	//Если НЕ ЗначениеЗаполнено(ОбъектЗаполнения.КодВПлатежнойСистемеДоРеформыБанковскихСчетов) Тогда
	//	ОбъектЗаполнения.КодВПлатежнойСистемеДоРеформыБанковскихСчетов = УправлениеДенежнымиСредствамиКлиентСервер.ПолучитьКодБанкаВПлатежнойСистеме(СтруктураДанных.КодБанкаДоРеформы);
	//КонецЕсли;
	
	ФормаНовогоЭлемента.Открыть();
	
КонецПроцедуры
